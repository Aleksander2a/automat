package automat
public
	with base_types;
	with sei;
	
	-------------------------------------------------
	-------------------- Devices --------------------
	-------------------------------------------------
	device panel
		features
			numer_out: out data port Base_Types::Integer_16;
			bus_access: requires bus access bus_connection;
	end panel;

	device implementation panel.impl
	end panel.impl;

	device silownik_spirali
		features
			ruch_spirala_in: in event port;
			bus_access: requires bus access bus_connection;
	end silownik_spirali;

	device implementation silownik_spirali.impl
	end silownik_spirali.impl;

	device oswietlenie
		features
			jasnosc_aktualna_out: out data port Base_Types::Integer_16;
			jasnosc_zadana_out: out data port Base_Types::Integer_16;
			swiatlo_in: in event port;
			bus_access: requires bus access bus_connection;
	end oswietlenie;

	device implementation oswietlenie.impl
	end oswietlenie.impl;

	device licznik_monet
		features
			wartosc_monet_out: out data port Base_Types::Integer_16;
			bus_access: requires bus access bus_connection;
	end licznik_monet;

	device implementation licznik_monet.impl
	end licznik_monet.impl;
	
	device podajnik_monet
		features
			wydaj_monety_in: in event port;
			bus_access: requires bus access bus_connection;
	end podajnik_monet;

	device implementation podajnik_monet.impl
	end podajnik_monet.impl;

	device chlodziarka
		features
			temperatura_aktualna_out: out data port Base_Types::Integer_16;
			temperatura_zadana_out: out data port Base_Types::Integer_16;
			chlodzenie_in: in event port;
			bus_access: requires bus access bus_connection;
	end chlodziarka;

	device implementation chlodziarka.impl
	end chlodziarka.impl;

	device wyswietlacz
		features
			wyswietl_in: in data port Base_Types::Integer_16;
			bus_access: requires bus access bus_connection;
	end wyswietlacz;

	device implementation wyswietlacz.impl
	end wyswietlacz.impl;
	
	-------------------------------------------------
	-------------------- Threads --------------------
	-------------------------------------------------
	thread wyswietl
		features
			wyswietl_in: in data port Base_Types::Integer_16;
			wyswietl_out: out data port Base_Types::Integer_16;
	end wyswietl;

	thread implementation wyswietl.impl
		properties
			Dispatch_protocol => Aperiodic;
			Compute_Execution_Time => 1 ms .. 2 ms;
	end wyswietl.impl;

	thread zaplac
		features
			numerek_in: in data port Base_Types::Integer_16;
			wartosc_monet_in: in data port Base_Types::Integer_16;
			panel_out: out data port Base_Types::Integer_16;
			wartosc_reszty_out: out data port Base_Types::Integer_16;
			ruch_spirala_out: out event port;
	end zaplac;

	thread implementation zaplac.impl
		properties
			Dispatch_protocol => Aperiodic;
			Compute_Execution_Time => 5 ms .. 15 ms;
	end zaplac.impl;

	thread wydaj
		features
			wartosc_reszty_in: in data port Base_Types::Integer_16;
			wydaj_monety_out: out event port;
	end wydaj;

	thread implementation wydaj.impl
		properties
			Dispatch_protocol => Aperiodic;
			Compute_Execution_Time => 3 ms .. 5 ms;
	end wydaj.impl;

	thread przyciski
		features
			numer_in: in data port Base_Types::Integer_16;
			numer_out: out data port Base_Types::Integer_16;
	end przyciski;

	thread implementation przyciski.impl
		properties
			Dispatch_protocol => Aperiodic;
			Compute_Execution_Time => 1 ms .. 2 ms;
	end przyciski.impl;
	
	thread chlodzenie
		features
			temperatura_aktualna_in: in data port Base_Types::Integer_16;
			temperatura_zadana_in: in data port Base_Types::Integer_16;
			chlodz_out: out event port;
	end chlodzenie;

	thread implementation chlodzenie.impl
		properties
			Dispatch_protocol => Aperiodic;
			Compute_Execution_Time => 1 ms .. 3 ms;
	end chlodzenie.impl;
	
	thread swiatlo
		features
			jasnosc_aktualna_in: in data port Base_Types::Integer_16;
			jasnosc_zadana_in: in data port Base_Types::Integer_16;
			swiec_out: out event port;
	end swiatlo;

	thread implementation swiatlo.impl
		properties
			Dispatch_protocol => Aperiodic;
			Compute_Execution_Time => 1 ms .. 3 ms;
	end swiatlo.impl;
	
	---------------------------------------------
	-------------------- Bus --------------------
	---------------------------------------------
	bus bus_connection
	end bus_connection;

	bus implementation bus_connection.impl
		properties
  			SEI::BandWidthCapacity => 2.0 MBytesps;
  			SEI::BandWidthBudget => 200.0 KBytesps;
	end bus_connection.impl;
	
	------------------------------------------------
	-------------------- Memory --------------------
	------------------------------------------------
	memory ram
		features
			bus_access: requires bus access bus_connection;
	end ram;
	
	---------------------------------------------------
	-------------------- Processor --------------------
	---------------------------------------------------
	processor main_processor
		features
			bus_access: requires bus access bus_connection;
	end main_processor;

	processor implementation main_processor.impl
	end main_processor.impl;
	
	processor second_processor
		features
			bus_access: requires bus access bus_connection;
	end second_processor;

	processor implementation second_processor.impl
	end second_processor.impl;
	
	-------------------------------------------------
	-------------------- Processes --------------------
	-------------------------------------------------
	process controller
		features
			numer_in: in data port Base_Types::Integer_16;
			wartosc_monet_in: in data port Base_Types::Integer_16;
			wyswietl_out: out data port Base_Types::Integer_16;
			ruch_spirala_out: out event port;
			wydaj_monety_out: out event port;
	end controller;

	process implementation controller.impl
		subcomponents
			thread_wyswietl: thread wyswietl.impl;
			thread_zaplac: thread zaplac.impl;
			thread_wydaj: thread wydaj.impl;
			thread_przyciski: thread przyciski.impl;
		connections
			wydaj_reszta_conn: port thread_wydaj.wydaj_monety_out -> wydaj_monety_out;
			przycisk_in_conn: port numer_in -> thread_przyciski.numer_in;
			pokarz_conn: port thread_wyswietl.wyswietl_out -> wyswietl_out;
			zaplac_conn_n_out: port thread_przyciski.numer_out -> thread_zaplac.numerek_in;			
			zaplac_conn_panel: port thread_zaplac.panel_out -> thread_wyswietl.wyswietl_in;			
			zaplac_conn_spirala: port thread_zaplac.ruch_spirala_out -> ruch_spirala_out;
			wartosc_monet_conn_1: port wartosc_monet_in -> thread_zaplac.wartosc_monet_in;
			wartosc_monet_conn_2: port thread_zaplac.wartosc_reszty_out -> thread_wydaj.wartosc_reszty_in;
	end controller.impl;
	
	process second_controller
		features
			temperatura_aktualna_in: in data port Base_Types::Integer_16;
			temperatura_zadana_in: in data port Base_Types::Integer_16;
			jasnosc_aktualna_in: in data port Base_Types::Integer_16;
			jasnosc_zadana_in: in data port Base_Types::Integer_16;
			swiatlo_out: out event port;
			chlodzenie_out: out event port;
	end second_controller;

	process implementation second_controller.impl
		subcomponents
			thread_chlodzenie: thread chlodzenie.impl;
			thread_swiatlo: thread swiatlo.impl;
		connections
			chlodzenie_conn: port thread_chlodzenie.chlodz_out -> chlodzenie_out;
			swiecenie_conn: port thread_swiatlo.swiec_out -> swiatlo_out;
			temp_akt_conn: port temperatura_aktualna_in -> thread_chlodzenie.temperatura_aktualna_in;
			temp_zad_conn: port temperatura_zadana_in -> thread_chlodzenie.temperatura_zadana_in;			
			jasnosc_akt_conn: port jasnosc_aktualna_in -> thread_swiatlo.jasnosc_aktualna_in;
			jasnosc_zad_conn: port jasnosc_zadana_in -> thread_swiatlo.jasnosc_zadana_in;
	end second_controller.impl;

	------------------------------------------------
	-------------------- System --------------------
	------------------------------------------------
	system automat
	end automat;

	system implementation automat.impl
		subcomponents
			device_panel: device panel;
			device_silownik_spirali: device silownik_spirali;
			device_oswietlenie: device oswietlenie;
			device_licznik_monet: device licznik_monet;
			device_chlodziarka: device chlodziarka;
			device_wyswietlacz: device wyswietlacz;
			device_podajnik_monet: device podajnik_monet;
			process_controller: process controller;
			process_second_controller: process second_controller.impl;
			processor_main_processor: processor main_processor.impl;
			processor_second_processor: processor second_processor.impl;
			memory_ram: memory ram;
			bus_bus_connection: bus bus_connection.impl;
		connections
			-- Controller
			panel_conn_in: port device_panel.numer_out -> process_controller.numer_in;
			silownik_conn: port process_controller.ruch_spirala_out -> device_silownik_spirali.ruch_spirala_in;
			licznik_monet_wartosc_conn: port device_licznik_monet.wartosc_monet_out -> process_controller.wartosc_monet_in;
			wyswitlacz_conn: port process_controller.wyswietl_out -> device_wyswietlacz.wyswietl_in;
			reszta_conn: port process_controller.wydaj_monety_out -> device_podajnik_monet.wydaj_monety_in;
			-- Second controller
			chlodzenie_conn: port process_second_controller.chlodzenie_out -> device_chlodziarka.chlodzenie_in;
			swiecenie_conn: port process_second_controller.swiatlo_out -> device_oswietlenie.swiatlo_in;
			temp_akt_conn: port device_chlodziarka.temperatura_aktualna_out -> process_second_controller.temperatura_aktualna_in;
			temp_zad_conn: port device_chlodziarka.temperatura_zadana_out -> process_second_controller.temperatura_zadana_in;			
			jasnosc_akt_conn: port device_oswietlenie.jasnosc_aktualna_out -> process_second_controller.jasnosc_aktualna_in;
			jasnosc_zad_conn: port device_oswietlenie.jasnosc_zadana_out -> process_second_controller.jasnosc_zadana_in;
			-- BusConnection
			bus_panel_zewnetrzny: bus access bus_bus_connection -> device_panel.bus_access;
			bus_silownik_spirali: bus access bus_bus_connection -> device_silownik_spirali.bus_access;
			bus_oswietlenie: bus access bus_bus_connection -> device_oswietlenie.bus_access;
			bus_licznik_monet: bus access bus_bus_connection -> device_licznik_monet.bus_access;
			bus_chlodziarka: bus access bus_bus_connection -> device_chlodziarka.bus_access;
			bus_wyswietlacz: bus access bus_bus_connection -> device_wyswietlacz.bus_access;
			bus_podajnik_monet: bus access bus_bus_connection -> device_podajnik_monet.bus_access;
			bus_memory: bus access bus_bus_connection -> memory_ram.bus_access;
			bus_processor: bus access bus_bus_connection -> processor_main_processor.bus_access;
			bus_second_processor: bus access bus_bus_connection -> processor_second_processor.bus_access;
		properties
  			Actual_Connection_Binding  => (reference(bus_bus_connection)) applies to panel_conn_in;
  			Actual_Connection_Binding  => (reference(bus_bus_connection)) applies to silownik_conn;
  			Actual_Connection_Binding  => (reference(bus_bus_connection)) applies to licznik_monet_wartosc_conn;
  			Actual_Connection_Binding  => (reference(bus_bus_connection)) applies to wyswitlacz_conn;
  			Actual_Connection_Binding  => (reference(bus_bus_connection)) applies to reszta_conn;
  			Actual_Processor_Binding => (reference(processor_main_processor))  applies to process_controller;
  			Actual_Processor_Binding => (reference(processor_second_processor))  applies to process_second_controller;
  			Actual_Memory_Binding => (reference(memory_ram))applies to process_controller;
  			Actual_Memory_Binding => (reference(memory_ram))applies to processor_second_processor;
	end automat.impl;
end automat;